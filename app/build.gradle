plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "org.sonarqube" version "3.2.0"
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id "io.gitlab.arturbosch.detekt" version "$detekt_version"
    id 'com.google.gms.google-services'
}

apply from: "$project.rootDir/tools/sonar-jacoco-multi-flavors.gradle"
apply from: "$project.rootDir/versions.gradle"
apply from: "$project.rootDir/tools/detekt.gradle"

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "fr.stephanesallaberry.news.android"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 2
        versionName "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                    excludes = ['jdk.internal.*']
                }
            }
        }
        animationsDisabled = true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "env"

    productFlavors {

        dev {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            def API_BASE_URL = "https://api.thecatapi.com"
            buildConfigField "String", "API_BASE_URL", "\"${API_BASE_URL}/v1/\""
        }

        staging {
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            def API_BASE_URL = "https://api.thecatapi.com"
            buildConfigField "String", "API_BASE_URL", "\"${API_BASE_URL}/v1/\""
        }

        prod {
            def API_BASE_URL = "https://api.thecatapi.com"
            buildConfigField "String", "API_BASE_URL", "\"${API_BASE_URL}/v1/\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/licenses/**']
        }
        resources {
            excludes += ['**/attach_hotspot_windows.dll', 'META-INF/licenses/**', 'META-INF/AL2.0', 'META-INF/LGPL2.1']
        }
    }

}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.arch.core:core-runtime:2.1.0'

    // Koin core features
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    // Koin test features
    testImplementation "io.insert-koin:koin-test:$koin_version"
    // Koin main features for Android (Scope,ViewModel ...)
    implementation "io.insert-koin:koin-android:$koin_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-runtime-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Retrofit
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    debugImplementation "com.github.chuckerteam.chucker:library:3.5.2"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:3.5.2"
    implementation('com.github.ihsanbal:LoggingInterceptor:3.1.0') {
        exclude group: 'org.json', module: 'json'
    }

    // log
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Image loading
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // MVI architecture
    implementation("org.orbit-mvi:orbit-core:$orbit_version")
    implementation("org.orbit-mvi:orbit-viewmodel:$orbit_version")
    testImplementation("org.orbit-mvi:orbit-test:$orbit_version")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.test:rules:1.4.0"

    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"

    //For runBlockingTest, CoroutineDispatcher etc.
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"
    //For InstantTaskExecutorRule
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    androidTestImplementation 'com.kaspersky.android-components:kaspresso:1.2.1'

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"

    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")
    implementation 'com.google.firebase:firebase-analytics'
}

ktlint{
    android = true
}
